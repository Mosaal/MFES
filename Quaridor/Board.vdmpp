class Board
	types
		public Matrix = seq of seq of Space;

	instance variables
		private board: Matrix := [[], [], [], [], [], [], [], [], []];
	
	operations
		-- Constructor
		public Board: () ==> Board
		Board() == (
			for all r in set { 1, ..., 9 } do (
				for all c in set { 1, ..., 9 } do (
					board(r)(c) := new Space(0, r, c)
				)
			);
			
			return self
		);
		
		-- Getters
		public getBoard: () ==> Matrix
		getBoard() == return board
		post RESULT = board;
		
		-- Setters
		public setValue: int * int * int ==> ()
		setValue(v, r, c) == board(r)(c).setValue(v)
		pre v in set { 0, 1, 2 }
		and r in set { 1, ..., 9 }
		and c in set { 1, ..., 9 };
		
		-- Logic functions
		public movePiece: int * int * Player ==> bool
		movePiece(r, c, pl) == (
			dcl ret: bool := false;
		
			if r = pl.getRow() then
				if c = pl.getCol() then
					return ret;
			
			if board(r)(c).getValue() = 0 then (
				if r = pl.getRow() then (
					if c < pl.getCol() then (
						if board(r)(c).getPathLeft() = false
							then ret := true
					) else (
						if board(r)(c).getPathRight() = false
							then ret := true
					)
				);
				
				if c = pl.getCol() then (
					if r < pl.getRow() then (
						if board(r)(c).getPathUp() = false
							then ret := true
					) else (
						if board(r)(c).getPathDown() = false
							then ret := true
					)
				)
			);
			
			return ret
		)
		pre r in set { 1, ..., 9 }
		and c in set { 1, ..., 9 };
		
		
		public placeWall: int * int * int ==> bool
		placeWall(r, c, dir) == (		
			-- UP = 1;
			-- DOWN = 2;
			-- LEFT = 3;
			-- RIGHT = 4;
			
			-- Se estiver nos limites do tabuleiro, algumas direcções são inválidas
			if r = 1 and dir = 1 then
				return false;
			if r = 9 and dir = 2 then
				return false;
			if c = 1 and dir = 3 then
				return false;
			if c = 9 and dir = 4 then
				return false;
			
			-- Ver se os caminhos adjacentes estão vazios
			if dir = 1 or dir = 2 then(
					if board(r)(c).getPathRight() = true then
						return false;
			) else (
					if board(r)(c).getPathDown() = true then
						return false;
			);
			
			-- Ver se os caminhos em largura estão vazios
			if dir = 1 then 
				if board(r - 1)(c).getPathRight() = true then
						return false;
			if dir = 2 then 
				if board(r + 1)(c).getPathRight() = true then
						return false;
			if dir = 3 then 
				if board(r)(c - 1).getPathDown() = true then
						return false;
			if dir = 4 then 
				if board(r)(c + 1).getPathDown() = true then
						return false;
						
						
			-- Verificar que a nova parede não atravessa transversalmente outra parede
			if dir = 1 then
				if board(r - 1)(c).getCorner() = true then 
					return false;
			if dir = 2 or dir = 4 then 
				if board(r)(c).getCorner() = true then 
					return false;
			if dir = 3 then
				if board(r)(c - 1).getCorner() = true then 
					return false;
					
					
			-- Todos os caminhos estão livres, pode-se acrescentar uma parede
			if dir = 1 then (
				board(r)(c).setPathRight(true);
				board(r - 1)(c).setPathRight(true);
				board(r - 1)(c).setCorner(true);
				return true;
			);
			if dir = 2 then (
				board(r)(c).setPathRight(true);
				board(r + 1)(c).setPathRight(true);
				board(r)(c).setCorner(true);
				return true;
			);
			if dir = 3 then (
				board(r)(c).setPathDown(true);
				board(r)(c - 1).setPathDown(true);
				board(r)(c - 1).setCorner(true);
				return true;
			);
			if dir = 4 then (
				board(r)(c).setPathDown(true);
				board(r)(c + 1).setPathDown(true);
				board(r)(c).setCorner(true);
				return true;
			);

			return false;
		)
		pre r in set { 1, ..., 9 }
		and c in set { 1, ..., 9 }
		and dir in set { 1, 2, 3, 4 };
end Board